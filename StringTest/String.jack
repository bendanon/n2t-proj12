// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/12/String.jack

/**
 * Represents a String object. Implements the String type.
 */
class String {
    
    field Array m_arr;
    field int m_size;
    field int m_length;

    /** Constructs a new empty String with a maximum length of maxLength. */
    constructor String new(int maxLength) {
        if(maxLength > 0)
        {
            let m_arr = Array.new(maxLength);
            let m_size = maxLength;
            let m_length = 0;
        }
        return this;
    }

    /** De-allocates the string and frees its space. */
    method void dispose() {
        if(0 < m_size)
        {
            do m_arr.dispose();
        }
        return;
    }

    /** Returns the current length of this String. */
    method int length() {
        return m_length;
    }

    /** Returns the character at location j. */
    method char charAt(int j) {
        if(~(j < 0) & ~(m_length < j))
        {
            return m_arr[j];
        }
        
        return 0;
    }

    /** Sets the j'th character of this string to be c. */
    method void setCharAt(int j, char c) {
        if(~(j < 0) & ~(m_size < j))
        {
            let m_arr[j] = c;
        }
        return;
    }

    /** Appends the character c to the end of this String.
     *  Returns this string as the return value. */
    method String appendChar(char c) {
        if(m_length < m_size)
        {
            let m_arr[m_length] = c;
            let m_length = m_length + 1;
        }

        return this;
    }

    /** Erases the last character from this String. */
    method void eraseLastChar() {
        if(0 < m_length)
        {
            let m_length = m_length - 1;
        }
        return;
    }

    /** Returns the integer value of this String until the first non
     *  numeric character. */
    method int intValue() {
        var int len, val;
        var bool negative;
        let negative = false;

        if(charAt(0) = 45)
        {
            let negative = true;
            let len = 1;
        }

        while(len < m_length)
        {
            let val = (val*10) + charAt(len) - 48;
            let len = len + 1;
        }
        
        if(negative)
        {
            return 0 - val;
        }
    
        return val;
    }

    method String int2string(int n){
        var int lastDigit;
        var String ret;
        let lastDigit =  n - ((n / 10)*10);

        if(n < 10)
        {
            return appendChar(n + 48);
        }

        let ret = int2string(n / 10);
        return ret.appendChar(lastDigit + 48);
    }
    
    /** Sets this String to hold a representation of the given number. */
    method void setInt(int number) {
        while(0 < m_length)
        {
            do eraseLastChar();
        }
        if(number < 0)
        {
            do appendChar(45);
            do int2string(Math.abs(number));
        }
        else
        {
            do int2string(number);
        }
        return;
    }

    /** Returns the new line character. */
    function char newLine() {
        return 128;
    }

    /** Returns the backspace character. */
    function char backSpace() {
        return 129;
    }

    /** Returns the double quote (") character. */
    function char doubleQuote() {
        return 34;
    }
}
